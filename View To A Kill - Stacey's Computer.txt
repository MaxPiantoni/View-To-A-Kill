01  LOC = 25476 + 1 : IF PEEK (103) + PEEK (104) * 256 <> LOC THEN POKE LOC - 1,0: POKE 103,LOC - INT(LOC/256) *256: POKE 104,INT (LOC / 256): PRINT CHR$(4);"RUN VTAK"
02  REM LINE 01 LOADS PROGRAM AFTER HGR PAGE 2 AT 24576 ($6000) IN MEMORY. THERE IS MORE ROOM THERE

03 REM -------------------------------------------
04 REM - STACEY'S COMPUTER FROM A VIEW TO A KILL -
05 REM -    A SOFTWARE FORGERY BY MAX PIANTONI   -
06 REM -            WWW.MAXPIANTONI.COM          -
07 REM -------------------------------------------



10 REM - LOADING ----------------------

12 REM  TITLE TEXT
15      HOME : VTAB 2: PRINT " STACEY'S COMPUTER" : PRINT " (FROM A VIEW TO A KILL)" 
16      VTAB 5 : : PRINT " A SOFTWARE FORGERY" : PRINT " BY MAX PIANTONI" : VTAB 8 : PRINT " WWW.MAXPIANTONI.COM"   

20 REM  LOAD ASSETS INTO MEMORY
25      D$ = CHR$ (4)        
30      PRINT D$;"BLOAD MAP.PIC,A$4000"                 : REM Load map to HGR page 2 (A$2000 loads to page 1)
35      FS = 2048                                       : REM FS = Start position in memory for the font shape table. We'll need to point to this location when using the font.
40      PRINT D$;"BLOAD STANDARD.FONT,A";FS             : REM Load the font shape table into memory
45      CS = 4000                                       : REM Set the position in the memory to load the circle shape table. We'll need to point to this location when using the circles.
50      PRINT D$;"BLOAD CIRCLE.SHAPES,A";CS             : REM Load the font shape table into memory    



60  REM - GRAPH SCREEN ----------------

62 REM Setup Plots
64     DIM PCOUNT(2) 
66     PCOUNT(0) = 32 :  PCOUNT(1) = 31  : PCOUNT(2) = 31         : REM The number of sets of x,y coords in each graph (each x,y set counts as 1. So this is half of the number of data points per plot) 

70  REM  - LOOP BACK TO THIS LINE WHEN RETURNING TO THE GRAPH SCREEN FROM THE MAP SCREEN -

80  REM Graphics Setup
82      HGR                                             : REM Transition to Hi-Res Graphics
84      POKE 49234,0                                    : REM Hide the text window
86      HCOLOR = 3                                      : REM Set the color we will draw in (white)
88      SX = 279 : SY = 191                             : REM The width and height of the screen in points

100 REM Draw horizontal graph Lines - in the film these are drawn first, in a ping-pong pattern
105     GAP = 31                                        : REM The Y position for the midpoint of the first graph  
110     YP = GAP                                        : REM Used to calculate the distance between the graphs on screen
115     HPLOT 0,YP TO SX,YP
120     YP = YP + (GAP * 2) 
125     HPLOT SX,YP TO 0,YP
130     YP = YP + (GAP * 2) 
135     HPLOT 0,YP TO SX,YP
                                                           
300 REM Read and plot data on the graph lines
301     RESTORE                                         : REM Reset data list to the beginning
305     YP = 31                                         : REM Set the midpoint for the first graph
310     FOR I = 0 TO 2                                  : REM Plot each graph
315         READ A,B                                    : REM Read data for first point              
320         FOR J = 1 TO (PCOUNT(I) - 1)                : REM Plot each point
325             READ C,D                                : REM read data for second point                                              
330             HPLOT A,(B + YP)                        : REM Plot start point
335             HPLOT C,(D + YP)                        : REM Plot end point
340             HPLOT A,(B + YP) TO C,(D + YP)          : REM Draw line
345             A = C : B = D                           : REM Set the end point of this line to be the start point for the next line
350         NEXT J
355      YP = YP + (GAP * 2)                            : REM Set the midpoint for the next graph    
360      NEXT I                                     

400      POKE 232,FS - INT (FS / 256) * 256              : REM Set the flags for the position of the font shape table in memory   
405      POKE 233, INT (FS / 256)                        : REM Set the flags for the position of the font shape table in memory  

600 REM Draw the line of text below the graph
605    S$ = "d = 9km                        m = 2.5"    : REM The string we will write to the screen                     
610    SCALE = 1: ROT = 0                               : REM Shape table drawing setup
615    TX = 0 : TY = 183                                : REM The start position for the drawing of the shapes
620    FOR V = 1 TO LEN(S$)                             : REM Loop through the string
625         SX$ = MID$(S$,V)                            : REM Get the next character from the string
630         C = (ASC(SX$) - 31)                         : REM Use the ascii value of the character and offset to deterimine the shape table index of the character to be drawn
635         DRAW C AT TX,TY                             : REM Draw the character from the shape table 
640         TX = TX + 7                                 : REM Advance the cursor position    
650    NEXT V                   



699 REM - MAP SCREEN ------------------

700 REM Transition to the map page on keyboard input
704     POKE 49168,0                                    : REM Clear the keyboard so we can detect input
705     IF PEEK(49152) < 128 GOTO 705                   : REM Wait for user to press a key on the keyboard (49152 contains character + 128)
706     POKE 49168,0                                    : REM Clear the keyboard so we can detect input again later
710     CALL -3086                                      : REM Clear screen to black as in movie
715     POKE 230,64 : POKE 49237,1                      : REM Set plotting page to high res graphics page 2 & display High res graphics page 2    

800 REM  Set shape table flags to point at the position of the circle shape table in memory 
805      POKE 232, CS - INT (CS / 256) * 256            : REM Set the flags for the position of the circle shape table in memory
815      POKE 233, INT (CS / 256)                       : REM Set the flags for the position of the circle shape table in memory 

900 REM Draw and animate the circles
905      FOR U = 0 TO 2 STEP 1                          : REM Loop circle animations (iterator is reset before loop for endless animation)
906      POKE 49168,0                                   : REM Clear the keyboard in case the user has struck multiple keys
910         TS = 4                                      : REM Which shape from the shape table are we drawing?   
915         X = 149 : Y = 34                            : REM The centre position of the circles
920         FOR C = 3 TO 1 STEP -1                      : REM Loop for drawing the 3 different circles
925             FOR F = 0 TO 7 STEP 1                   : REM Loop for flashing each circle a number of times         
930             RT = 0                                  : REM Initial Rotation of the circle quadrant 
935                 SCALE = 1: ROT = RT                 : REM Shape table drawing setup
940                 YOFFSET = 7 + (7 * C)               : REM Calculate the Y position at which to start plotting the circle     
945                 XDRAW TS AT X,Y - YOFFSET           : REM Draw first quadrant around x,y co-ords (with y offset)
950                     FOR I = 1 TO 3 STEP 1           : REM Loop for drawing other three quadrants    
955                         RT = RT + 16                : REM Add 90 degrees to cumulative rotation
960                         ROT = RT                    : REM Rotate the shape by 90 degrees
965                         XDRAW TS                         
970                     NEXT I    
975             IF F <> 0 THEN HCOLOR = 0               : REM If it's not the first flash the centrepoint is drawn black so it's invisible
980             HPLOT X,Y                               : REM Draw a dot a the centre point             
981             HCOLOR = 3                              : REM Reset drawing color so circle is always drawn white
982             FOR D = 0 TO 10 STEP 1 : NEXT D         : REM Delay for animation
983             NEXT F
984         TS = TS - 1                                 : REM set the circle to be drawn to the previous shape in the shape table
985         IF U > 0 THEN IF PEEK(49152) > 128 GOTO 70  : REM If the user has pressed a key, loop back to map screen setup. (49152 contains character + 128). Ignores keypresses on the first iteration (presumed accidental).
986         NEXT C
987         FOR D = 0 TO 50 STEP 1 : NEXT D             : REM Delay for animation
988     U = 1                                           : REM Reset the iterator so it loops forever
989     NEXT U                                          : REM Loop animation



999 REM GRAPH DATA --------------------

1000 REM Data for plot line 1 - a series of x,y positions
1005    DATA    30,0,       35,2,       40,0,       45,-2      
1010    DATA    55,0,       56,-25,     58,25,      59,-20
1015    DATA    60,21,      62,-15,     64,15,      66,-10     
1020    DATA    69,10,      72,-8,      74,8,       78,-5  
1025    DATA    80,2,       90,-2,      92,0,       94,25      
1030    DATA    95,-25,     96,22,      98,-18,     98,-18     
1035    DATA    100,10,     103,-8,     105,5,      108,-5     
1040    DATA    118,3,      120,0,      140,3,      155,0    

2000 REM Data for plot line 2 - a series of x,y positions   
2005    DATA    3,0,        12,-1,      21,2,       33,-1      
2010    DATA    42,3,       50,-2,      65,0,       67,25      
2015    DATA    71,-23,     71,19,      74,-10,     76,9       
2020    DATA    78,-1,      81,3,       88,-1,      94,3
2025    DATA    97,0,       97,-21,     98,21,      99,-21     
2030    DATA    100,23,     102,-13,    103,18,     105,-11          
2035    DATA    106,8,      109,-3,     111,3,      113,-2     
2040    DATA    117,3,      132,-2,     143,0

3000 REM Data for plot line 3 - a series of x,y positions   
3005    DATA    0,0,        12,-1,      25,1,       34,-3     
3010    DATA    42,1,       54,1,       63,-2,      77,0                  
3015    DATA    83,-27,     83,25,      85,-17,     87,13      
3020    DATA    88,-9,      91,7,       93,-3,      97,2       
3025    DATA    105,4,      109,0,      109,-23,    111,22     
3030    DATA    110,-24,    112,24,     114,-15,    115,15
3035    DATA    117,-8,     119,5,      121,-2,     123,1      
3040    DATA    128,-1,     131,2,      140,-1    
