01  LOC = 25476 + 1 : IF PEEK (103) + PEEK (104) * 256 <> LOC THEN POKE LOC - 1,0: POKE 103,LOC - INT(LOC/256) *256: POKE 104,INT (LOC / 256): PRINT CHR$(4);"RUN VTAK"
05  REM LINE 01 LOADS PROGRAM AFTER HGR PAGE 2 AT 24576 ($6000) IN MEMORY. THERE IS MORE ROOM THERE

10 REM STACEY'S COMPUTER FROM A VIEW TO A KILL, RECREATED BY MAX PIANTONI WWW.MAXPIANTONI.COM

11 REM  TITLE TEXT
15      HOME : VTAB 2: PRINT " STACEY'S COMPUTER" : PRINT " (FROM A VIEW TO A KILL)" 
16      VTAB 5 : : PRINT " A SOFTWARE FORGERY" : PRINT " BY MAX PIANTONI" : VTAB 8 : PRINT " WWW.MAXPIANTONI.COM"   

20 REM  LOAD ASSETS INTO MEMORY
21      D$ = CHR$ (4)        

24 REM  Load the map image to HGR page 2
25      PRINT D$;"BLOAD MAP.PIC,A$4000"                 : REM Load map to HGR page 2 (A$2000 loads to page 1)

30 REM  Load the font shape table from the floppy. The font used is ']SMALL STANDARD' from the Beagle Bros. Apple Mechanic.     
32      FS = 2048                                       : REM FS = Start position in memory for the font shape table
34      PRINT D$;"BLOAD STANDARD.FONT,A";FS             : REM Load the font shape table into memory
36      POKE 232,FS - INT (FS / 256) * 256              : REM Set the flags for the position of the font shape table in memory   
38      POKE 233, INT (FS / 256)                        : REM Set the flags for the position of the font shape table in memory  

40 REM  Load the circle shape table into memory
45      CS = 4000                                       : REM Set the position in the memory to load the circle shape table
50      PRINT D$;"BLOAD CIRCLE.SHAPES,A";CS             : REM Load the font shape table into memory    



60  REM GRAPH SCREEN

61  REM Graphics Setup
65      HGR                                             : REM Transition to Hi-Res Graphics
70      POKE 49234,0                                    : REM Hide the text window
75      HCOLOR = 3                                      : REM Set the color we will draw in (white)
80      SX = 279 : SY = 191                             : REM The width and height of the screen in points

100 REM Draw horizontal graph Lines - in the film these are drawn first, in a ping-pong pattern
105     GAP = 31                                        : REM The Y position for the midpoint of the first graph  
110     YP = GAP                                        : REM Used to calculate the distance between the graphs on screen
115     HPLOT 0,YP TO SX,YP
120     YP = YP + (GAP * 2) 
125     HPLOT SX,YP TO 0,YP
130     YP = YP + (GAP * 2) 
135     HPLOT 0,YP TO SX,YP

200 REM Setup Plots
220     DIM PCOUNT(2) 
225     PCOUNT(0) = 32 :  PCOUNT(1) = 32  : PCOUNT(2) = 32         : REM The number of sets of x,y coords in each graph
230                                                                : REM  (each x,y set counts as 1. So this is half of the number of data points per plot) 

300 REM Read and plot data on the graph lines
305     YP = 31                                         : REM Set the midpoint for the first graph
310     FOR I = 0 TO 2                                  : REM Plot each graph
315         READ A,B                                    : REM Read data for first point              
320         FOR J = 1 TO (PCOUNT(I) - 1)                : REM Plot each point
325             READ C,D                                : REM read data for second point                                              
330             HPLOT A,(B + YP)                        : REM Plot start point
335             HPLOT C,(D + YP)                        : REM Plot end point
340             HPLOT A,(B + YP) TO C,(D + YP)          : REM Draw line
345             A = C : B = D                           : REM Set the end point of this line to be the start point for the next line
350         NEXT J
355      YP = YP + (GAP * 2)                            : REM Set the midpoint for the next graph    
360      NEXT I                                     

600 REM Draw the line of text below the graph
605    S$ = "d = 9km                        m = 2.5"    : REM The string we will write to the screen                     
610    SCALE = 1: ROT = 0                               : REM Shape table drawing setup
615    TX = 0 : TY = 183                                : REM The start position for the drawing of the shapes
620    FOR V = 1 TO LEN(S$)                             : REM Loop through the string
625         SX$ = MID$(S$,V)                            : REM Get the next character from the string
630         C = (ASC(SX$) - 31)                         : REM Use the ascii value of the character and offset to deterimine the shape table index of the character to be drawn
635         DRAW C AT TX,TY                             : REM Draw the character from the shape table 
640         TX = TX + 7                                 : REM Advance the cursor position    
650    NEXT V                   



699 REM MAP SCREEN

700 REM Transition to the map page
705     GET KB$                                         : REM Wait for user to press a key on the keyboard
710     CALL -3086                                      : REM Clear screen to black as in movie
715     POKE 230,64 : POKE 49237,1                      : REM Set plotting page to high res graphics page 2 & display High res graphics page 2    

800 REM  Set the flags for the position of the font shape table in memory 
805      POKE 232, CS - INT (CS / 256) * 256            : REM Set the flags for the position of the font shape table in memory
815      POKE 233, INT (CS / 256)                       : REM Set the flags for the position of the font shape table in memory 

900 REM Draw and animate the circles
905      FOR U = 0 TO 10000 STEP 1                      : REM Loop circle animations for a while
910         TS = 4                                      : REM Which shape from the shape table are we drawing?   
915         X = 149 : Y = 34                            : REM The centre position of the circles
920         FOR C = 3 TO 1 STEP -1                      : REM Loop for drawing the 3 different circles
925             FOR F = 0 TO 7 STEP 1                   : REM Loop for flashing each circle a number of times         
930             RT = 0                                  : REM Initial Rotation of the circle quadrant 
935                 SCALE = 1: ROT = RT                 : REM Shape table drawing setup
940                 YOFFSET = 7 + (7 * C)               : REM Calculate the Y position at which to start plotting the circle     
945                 XDRAW TS AT X,Y - YOFFSET           : REM Draw first quadrant around x,y co-ords (with y offset)
950                     FOR I = 1 TO 3 STEP 1           : REM Loop for drawing other three quadrants    
955                         RT = RT + 16                : REM Add 90 degrees to cumulative rotation
960                         ROT = RT                    : REM Rotate the shape by 90 degrees
965                         XDRAW TS                         
970                     NEXT I    
975             IF F <> 0 THEN HCOLOR = 0               : REM If it's not the first flash the centrepoint is drawn black so it's invisible
980             HPLOT X,Y                               : REM Draw a dot a the centre point             
981             HCOLOR = 3                              : REM Reset drawing color so circle is always drawn white
982             FOR D = 0 TO 10 STEP 1 : NEXT D         : REM Delay for animation
983             NEXT F
984         TS = TS - 1                                 : REM set the circle to be drawn to the previous shape in the shape table
986         NEXT C
987         FOR D = 0 TO 50 STEP 1 : NEXT D             : REM Delay for animation
989     NEXT U                                          : REM Loop animation



1000 REM Data for plot line 1 - a series of x,y positions
1001    DATA    30,0,  35,2,   40,0,   45,-2,  55,0,   56,-25, 58,25,  59,-20
1002    DATA    60,21, 62,-15, 64,15,  66,-10, 69,10,  72,-8,  74,8,   78,-5
1003    DATA    80,2,  90,-2
1004    DATA    92,0,  94,25,  95,-25, 96,22,  98,-18, 98,-18, 100,10, 103,-8
1005    DATA    105,5, 108,-5, 118,3,  120,0,  140,3,  155,0    

2000 REM Data for plot line 2 - a series of x,y positions   
2001    DATA    30,0,  35,2,   40,0,   45,-2,  55,0,   56,-25, 58,25,  59,-20
2002    DATA    60,21, 62,-15, 64,15,  66,-10, 69,10,  72,-8,  74,8,   78,-5
2003    DATA    80,2,  90,-2
2004    DATA    92,0,  94,25,  95,-25, 96,22,  98,-18, 98,-18, 100,10, 103,-8
2005    DATA    105,5, 108,-5, 118,3,  120,0,  140,3,  155,0 

3000 REM Data for plot line 3 - a series of x,y positions   
3001    DATA    30,0,  35,2,   40,0,   45,-2,  55,0,   56,-25, 58,25,  59,-20
3002    DATA    60,21, 62,-15, 64,15,  66,-10, 69,10,  72,-8,  74,8,   78,-5
3003    DATA    80,2,  90,-2
3004    DATA    92,0,  94,25,  95,-25, 96,22,  98,-18, 98,-18, 100,10, 103,-8
3005    DATA    105,5, 108,-5, 118,3,  120,0,  140,3,  155,0 
